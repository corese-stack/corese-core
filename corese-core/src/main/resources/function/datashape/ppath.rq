#
# Data Shape Property Path Interpreter
# PP expressions are searched in graph ?shape 
#
# Olivier Corby - Wimmics Inria I3S - 2016-2019
#
prefix sh:   <http://www.w3.org/ns/shacl#> 


#
# SHACL Path Compiler from RDF graph to list expression
#

#
# Rewrite Property Path shape expression as a LDScript list
#
# ([sh:zeroOrMorePath rdf:rest] rdf:first)
# ->
# (sh:sequence ((sh:zeroOrMorePath rdf:rest) rdf:first))
# URI may be URI of a subpath such as:
# [sh:path ex:parent] .
# ex:parent sh:alternativePath (ex:father ex:mother)
# sh:path (ai:location [ sh:service (<http://dbpedia.org/sparql> rdf:type ) )

@import <ppathext.rq>

function sh:path(dt:graph ?shape, ?exp) {
  if (isURI(?exp) || isLiteral(?exp)) { ?exp }
  else {
    let ( 
        select ?shape ?exp ?q ?path  where {
            graph ?shape { 
               # rdf:rest is for a sequence
               # values ?q { 
               #     rdf:rest sh:inversePath sh:alternativePath 
               #     sh:zeroOrMorePath sh:oneOrMorePath sh:zeroOrOnePath  
               #     sh:service sh:ldpath }
                ?exp ?q ?path   filter (?q not in (rdf:first))
            }
        } ) {
          
    if (! bound(?q)) { return(error()) }   
    else if (?q = rdf:rest)    { return(xt:list(sh:sequence, sh:listsh(?shape, ?exp))) }
    else if (sh:extension(?exp, ?q)) { return (sh:extension(?shape, ?q, ?path)) }
    else { return(xt:list(?q, sh:path(?shape, ?path))) }
    }
  }
}


# [<http://dbpedia.org/sparql> (rdf:type)]
# test isBlank because a path element may be an URI which may have properties
# focus on local bnode 
function sh:extension(exp, q) {
    return (sh:statement(q) || sh:operator(q) || (isBlank(exp) && sh:usernamespace(q)))
}

function sh:statement(oper) {
    oper in (sh:service, sh:ldpath)
}

function sh:operator(oper) {
    sh:boolean(oper) || 
    oper in (sh:pattern, sh:nodeKind, sh:datatype, sh:minLength, sh:maxLength, sh:hasValue, sh:validURI)
}

function sh:boolean(oper) {
    oper in (sh:and, sh:or, sh:not)
}

function sh:usernamespace2(oper) {
    ! mapany(rq:strstarts, oper, xt:list(rdf:, rdfs:, owl:, sh:))
}

function sh:usernamespace(oper) {
    for (pref in xt:list(rdf:, rdfs:, owl:, sh:)) {
        if (strstarts(oper, pref)) {
            return (false)
        }
    } ;
    return (true)
}

#
# (us:location [sh:pattern 'dbpedia'] [<http://dbpedia.org/sparql> (rdf:type) ] ) 
#
function sh:extension(shape, oper, path) {
         if (sh:statement(oper))     { return(xt:list(oper,   sh:listsh(shape, path))) }
    else if (sh:operator(oper))      { return(sh:parseOperator(shape, oper, path)) }
    else if (sh:usernamespace(oper)) { return(xt:list(sh:service, xt:cons(oper, sh:listsh(shape, path)))) }
}



# oper = sh:pattern ; path = 'http://dbpedia' 
# return (sh:filter (sh:pattern 'http://dbpedia'))
function sh:parseOperator(shape, oper, path) {
    if (sh:boolean(oper), sh:parseBoolean(shape, oper, path),
        xt:list(sh:filter, xt:list(oper, path)))
}

# [sh:and ([sh:nodeKind sh:IRI] [sh:pattern 'dbpedia']) ]
# oper = sh:and ; path = list bnode 
function sh:parseBoolean(shape, oper, path) {
    let (list = sh:listsh(shape, path)) {
        xt:list(sh:filter, xt:list(sh:booleanoper(oper), list))
    }
}

function sh:booleanoper(oper) {
    if (oper = sh:and, sh:myand, if (oper = sh:or, sh:myor, sh:mynot))
}


#
# Rewrite recursively RDF list ?exp as a list
#
function dt:list sh:listsh(dt:graph ?shape, ?exp){
   let ( 
        select ?shape ?exp (aggregate(sh:path(?shape, ?e)) as ?l)  
        where { graph ?shape { ?exp rdf:rest*/rdf:first ?e }}) {
        return(?l)
    }
}

function dt:list sh:tolist(dt:graph ?shape, ?exp){
   let ( 
        select ?shape ?exp (aggregate(?e) as ?l)  
        where { graph ?shape { ?exp rdf:rest*/rdf:first ?e }}) {
        return(?l)
    }
}


#
# Return list of nodes related to node ?s by path ?p
# ?s is subject, ?p is PP expression
# ?s = us:John ; ?p = [sh:zeroOrMorePath foaf:knows]
#
function dt:list sh:path(dt:graph shape, s, p){    
    sh:path(shape, s, sh:getPath(shape, p), false)
}

#
# rewrite PP once as a list and record it
#
function sh:getPath(dt:graph shape, p){
   return (if (sh:hasConstraint(sh:path, p), 
     sh:getConstraint(sh:path, p), 
     sh:setConstraint(sh:path, p, sh:path(shape, p))))
}


# ********************************************************************************************

#
# Path expression interpreter
#


#
# Evaluate PP p as a list
# Return list of target nodes that match the path
# inv = true means within inverse path
#
function dt:list sh:path(dt:graph shape, node, exp, xsd:boolean inv){
    #xt:print("path:", set(nb = coalesce(nb + 1, 1))) ;
    sh:evalpath(shape, node, exp, inv, st:null)
}


#
# Public extension function to be used out of SHACL
#
function sh:evalpath(node, exp) {
    sh:evalpath(xt:graph(), node, exp, false, st:null)
}


# url = st:null || server URL
# if null, triple patterns are evaluated on local dataset
# if not null, triple patterns are evaluated on SPARQL endpoint url with service url { s p o }
#
function dt:list sh:evalpath(dt:graph shape, node, exp, xsd:boolean inv, url){
  if (isExtension(exp)) { # dt:list
     let ((oper path) = exp) {      
       return(funcall(oper, shape, node, path, inv, url))
     }  
  }
  else { # URI or *
     return(sh:pathProperty(shape, node, exp, inv, url))
  } 
}


# exp is a list of path expressions
function dt:list sh:zeroOrOnePath(dt:graph shape, node, exp, xsd:boolean inv, url){
    let (nodeList = sh:evalpath(shape, node, exp, inv, url)){
        if (xt:member(node, nodeList), 
            return(nodeList), 
            return(xt:cons(node, nodeList)))
    }
}

function dt:list sh:zeroOrMorePath(dt:graph shape, node, exp, xsd:boolean inv, url){
    sh:rec(shape, node, exp, xt:list(node), inv, url)
}

function dt:list sh:oneOrMorePath(dt:graph shape, node, exp, xsd:boolean inv, url){
    sh:rec(shape, node, exp, xt:list(), inv, url)
}

#
# ?p is a PP expression exp
# compute exp*
# ?res is the list of nodes already reached by exp*
# ?res prevents loops
#
function dt:list sh:rec(dt:graph shape, node, exp, dt:list res, xsd:boolean inv, url){
    let (nodeList = sh:evalpath(shape, node, exp, inv, url)){
        if (xt:size(nodeList) = 0){ return(res) }
        else {
            for (next in nodeList){
                if (! xt:member(next, res)){
                    xt:add(res, next) ; 
                    sh:rec(shape, next, exp, res, inv, url)
                }             
            } ;
            return(res)             
        }
    }
}


#
# s ^exp
# inverse PP expression
#
function dt:list sh:inversePath(dt:graph shape, node, exp, xsd:boolean inv, url){
    sh:evalpath(shape, node, exp, ! inv, url)
}


#
# (sh:sequence (exp .. exp))
# exp = (exp .. exp)
#
function dt:list sh:sequence(dt:graph shape, node, exp, xsd:boolean inv, url){
    if (inv) {
        return(sh:seqlist(shape, node, xt:reverse(exp), inv, url))
    }
    else{
        return(sh:seqlist(shape, node, exp, inv, url))
    }
}




#
# Recursive traversal of sequence list expList
# recurse on xt:rest(expList)
#
function dt:list sh:seqlist1(dt:graph shape, node, dt:list expList, xsd:boolean inv, url) {
    if (xt:size(expList) = 0) {
        # reach end of sequence: add target node subject
        return(xt:list(node))
    }
    else {
        let ((firstExp | restExpList) = expList,
              nodelist = sh:evalpath(shape, node, firstExp, inv, url), 
            res = xt:list()) {
            for (next in nodelist) { 
                # merge removes duplicate nodes
                set(res = xt:merge(res, sh:seqlist1(shape, next, restExpList, inv, url)))
            } ;
            return(res)
        }
    }
}

#
# Recursive traversal of sequence list expList
# recurse on xt:rest(expList)
#
function dt:list sh:seqlist(dt:graph shape, node, dt:list expList, xsd:boolean inv, url) {
    if (xt:size(expList) = 0) {
        # reach end of sequence: add target node subject
        return(xt:list(node))
    }
    else {
        let ((firstExp | restExpList) = expList,
              nodeList = sh:evalpath(shape, node, firstExp, inv, url)) { 
            return (sh:seqlistmap(shape, nodeList, restExpList, inv, url))
        }
    }
}

# if next exp is service, we can execute it with values nodeList for focus nodes
#
function dt:list sh:seqlistmap(dt:graph shape, nodeList, dt:list expList, xsd:boolean inv, url) {
    let (res = xt:list()) {
        for (next in nodeList) { 
            # merge removes duplicate nodes
            set(res = xt:merge(res, sh:seqlist(shape, next, expList, inv, url)))
        } ;
        return (res)
    }
}




# p = (sh:alternativePath (exp .. exp))
function dt:list sh:alternativePath(dt:graph shape, node, exp, xsd:boolean inv, url){
    let (expList = xt:get(exp, 1)) {
        return(mapmerge(sh:pathmap, expList, xt:list(shape), node, inv, url))
    }
}

function dt:list sh:pathmap(exp, dt:graph shape, node, xsd:boolean inv, url){
    sh:evalpath(shape, node, exp, inv, url)
}





function dt:list sh:pathProperty(shape, node, p, xsd:boolean inv, url){
    if (url = st:null) {
        if (inv) {
            return(sh:inverse(node, p))
        }
        else if (isLiteral(p)) { # p = *
            return (sh:star(node)) 
        }
        else {
            return(sh:predicate(node, p))    
        }

    }
    else if (inv) {
            return(sh:inverseService(node, p, url))
        }
        else if (isLiteral(p)) { # p = *
            return (sh:starService(node, url)) 
        }
        else {
            return(sh:predicateService(node, p, url))    
        }
}

function dt:list sh:predicate(?s, ?p){
    xt:objects(?s, ?p)
}

function dt:list sh:predicate1(?s, ?p){
    let (select ?s ?p (aggregate(?o) as ?l) where  { ?s ?p ?o }){
        return(?l)
    }
}

function dt:list sh:star(?s){
    let (select ?s (aggregate(?o) as ?l) where  { ?s ?p ?o }){
        return(?l)
    }
}

function dt:list sh:inverse(?o, ?p){
    xt:subjects(?p, ?o)
}

function dt:list sh:inverse1(?o, ?p){
    let (select ?o ?p (aggregate(?s) as ?l) where  { ?s ?p ?o }){
        return(?l)
    }
}

function dt:list sh:predicateService(?s, ?p, ?url){
    if (us:trace(), xt:print("predicate service:", ?url, ?s, ?p), true);
    if (isBlank(?s)) { return(xt:list()) } ;
    let (select ?s ?p ?url (aggregate(?o) as ?l) where  { service ?url { ?s ?p ?o } }){
    if (us:trace(), xt:print("list:", l), true);
        return(?l)
    }
}

function dt:list sh:starService(?s, ?url){
    if (us:trace(), xt:print("predicate service:", ?url, ?s), true);
    if (isBlank(?s)) { return(xt:list()) } ;
    let (select ?s ?url (aggregate(?o) as ?l) where  { service ?url { ?s ?p ?o } }){
    if (us:trace(), xt:print("list:", l), true);
        return(?l)
    }
}

function dt:list sh:inverseService(?o, ?p, ?url){
    if (isBlank(?o)) { return(xt:list()) } ;
    let (select ?o ?p ?url (aggregate(?s) as ?l) where  { service ?url { ?s ?p ?o } }){
        return(?l)
    }
}


