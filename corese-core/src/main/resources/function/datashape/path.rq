#
# SHACL Interpreter 
#
# Olivier Corby - Wimmics Inria I3S - 2016-2019
#

prefix sh:  <http://www.w3.org/ns/shacl#> 
prefix xsh: <http://www.w3.org/ns/shacl#> 
prefix msh: <http://ns.inria.fr/shacl/>

@import <sparql.rq>


#
# shape: shacl graph
# sh: current shape
# vis: true if report is needed
# nodeList:  list of target nodes of shape sh
# path: path expression
# 
function sh:path(shape, sh, vis, nodeList, path) {
    if (sh:trace(), xt:print("path:", nodeList, path), true);
  let (b1 = sh:path1(shape, sh, vis, nodeList, path), 
       b2 = sh:path2(shape, sh, vis, nodeList, path), 
       b3 = sh:path3(shape, sh, vis, nodeList, path),
       b4 = sh:pathpattern(shape, sh, vis, nodeList, path),
       b5 = sh:pathextension(shape, sh, vis, nodeList, path),
       b6 =      sh:sparql(shape, sh, vis, nodeList, path)
       ) {
       return (b1 && b2 && b3 && b4 && b5 && b6)
    }
}


#
# nodeList: target node list 
# p: the path
# oper=sh:nodeKind ; val=sh:IRI
#
function sh:path1(shape, sh, vis, nodeList, p) {
    let (res = true, cstList = sh:getConstraint(sh:path1, shape, sh)) {
        if (xt:size(cstList) > 0) {
            #xt:print("path:", oper, val, p, nodeList);
            for (s in dt:list(nodeList)) {
                for (o in sh:path(shape, s, p)) {
                    for ((oper val) in cstList) {
                        let (suc = coalesce(
                                    funcall(oper, shape, o, val), 
                                    false)) {
                                if (oper in (sh:property), true, 
                                        sh:report(oper, sh, if (oper = sh:node, val, st:null), shape, s, p, o, suc, vis)) ;
                                if (! suc) {
                                    set(res = false) 
                                }
                            }
                        }
                }
            }
        } ;
        return (res)
    }
}


# xsh:function [ name (value) ]
function sh:pathextension(shape, sh, vis, nodeList, p) {
    let (res = true, cstList = sh:getConstraint(sh:pathextension, shape, sh)) {
        if (xt:size(cstList) > 0) {
            #xt:print("path:", oper, val, p, nodeList);
            for (s in dt:list(nodeList)) {
                for (o in sh:path(shape, s, p)) {
                    for ((oper name value ternary) in cstList) {
                        let (suc = coalesce(
                             if (ternary, 
                                funcall(name, s, o, value), 
                                funcall(name, o, value)), 
                             false)) {
                                sh:report(oper, sh, name, shape, s, p, o, suc, vis) ;
                                if (! suc) {
                                    set(res = false) 
                                }
                            }
                        }
                }
            }
        } ;
        return (res)
    }
}

function sh:path2(shape, sh, vis, nodeList, p) {
    let (res = true) {
        for ((oper val) in sh:getConstraint(sh:path2, shape, sh)) {
            for (s in dt:list(nodeList)) {
                let (suc = coalesce(funcall(oper, shape, sh, vis, s, p, val),   false)) {
                    if (! suc) {
                        set(res = false) 
                    }
                }
            }
        } ;
        return (res)
    }
}


function sh:path3(shape, sh, vis, nodeList, p) {
    let (res = true) {
        for ((oper val disjoint amin amax) in sh:getConstraint(sh:path3, shape, sh)) {
            for (s in dt:list(nodeList)) {
                let (suc = coalesce(                    
                        sh:qualifiedValueShape(shape, sh, vis, s, p, val, disjoint, amin, amax) , 
                        false) ) {
                    if (! suc) {
                        set(res = false) 
                    }
                }
            }
        } ;
        return (res)
    }
}

# sh:cstpattern defined in constraint.rq
function sh:pathpattern (shape, sh, vis, nodeList, p) {
    let (res = true) {
        for ((oper flag) in sh:getConstraint(sh:cstpattern, shape, sh)) {  
            for (subject in dt:list(nodeList)) { 
                for (node in sh:path(shape, subject, p)) {
                    let (suc = !isBlank(node) && coalesce(
                            if (isURI(flag), regex(node, oper), regex(node, oper, flag)),
                            false)) {    
                        if (!suc) {
                            set(res = false) ;
                            sh:report(sh:pattern, sh, shape, subject, p, node, suc, vis)
                        }
                    }
                }
            }
        } ;
        return (res)
    }
}





# *******************************************


#
# Retrieve properties of constraint sh using a query, do it once
# they are stored as a list in a map by sh:getConstraint defined in core.rq
#

function sh:getPathShape(shape, sh, shapeList) {
    let (select ?shape ?sh ?shapeList (aggregate(xt:list(?oper, ?val)) as ?list) where {
            graph ?shape { 
                values ?shapeList {UNDEF}
                values ?oper { unnest(?shapeList) }
                ?sh ?oper ?val 
            filter if (?oper = sh:uniqueLang, sameTerm(?val, true), true)
            }
        } ) {
        return(list)
    }
}

function sh:pathextension(shape, sh) {
    sh:getShapeExtension(shape, sh, sh:getShape(sh:pathextension))
}
        
function sh:path1(shape, sh) {
    sh:getPathShape(shape, sh, sh:getShape(sh:path1))
}

function sh:path2(shape, sh) {
    sh:getPathShape(shape, sh, sh:getShape(sh:path2))
}

function sh:path3(shape, sh) {
    let (select ?shape ?sh 
        (aggregate(xt:list(sh:qualifiedValueShape, ?qsh, 
            coalesce(disjoint, false), coalesce(?min, 0), coalesce(?max, -1))) as ?list) 
        where {
            graph ?shape {  
                ?sh sh:qualifiedValueShape ?qsh
                optional { ?sh sh:qualifiedMinCount ?min }
                optional { ?sh sh:qualifiedMaxCount ?max }
                optional { ?sh sh:qualifiedValueShapesDisjoint ?disjoint }
            }
        } ) {
        return(list)
    }
}


#
# called when start
# within xt:focus(?shape, sh:sibling())
# record sibling qualifiedValueShape in a table
#
function sh:sibling() {
    for (select  ?qsh ?list where {
            select ?qsh (aggregate(xt:list(?sibling, ?path)) as ?list) 
            where {
                select distinct ?qsh ?sibling ?path
                where {
                    ?root sh:property [ sh:qualifiedValueShape ?qsh ];
                          sh:property [ sh:qualifiedValueShape ?sibling ; sh:path ?path ]  
                    filter (?sibling != ?qsh)
                }
            }
            group by ?qsh
        } ) {
            if (bound(?qsh), sh:setConstraint(sh:sibling, ?qsh, ?list), true) 
    } ;
    return (true)
}


# ************** operators


function sh:qualifiedValueShape (shape, sh, vis, s, p, qsh, disjoint, amin, amax) {
    let (list  = sh:qualified(shape, qsh, s, p),
         qlist = if (coalesce(disjoint, false), 
            # remove elements that are in sibling shapes
            mapfindlist(sh:disjoint, list, xt:list(shape), qsh, s), list),
         suc1 = xt:size(qlist) >= amin, 
         suc2 = if (amax = -1, true, xt:size(qlist) <= amax))
    {
        sh:report(sh:qualifiedMinCount, sh, shape, s, p, s, suc1, vis) ;
        sh:report(sh:qualifiedMaxCount, sh, shape, s, p, s, suc2, vis) ;
        return (suc1 && suc2)
    }
}

#
# test disjointness of subject ?s value ?o with ?qsh sibling shapes
# for each sibling shape ?sh, compute ?list of values and test ?o not in ?list
#
function sh:disjoint(o, shape, qsh, s){
   for ((sh, path) in sh:getConstraint(sh:sibling, qsh)){
        if (xt:member(o, sh:qualified(shape, sh, s, path))){
            return (false)
        }        
   } ;
   return (true)
}

#
# Return the list of value nodes that match qualified shape qsh 
#
function sh:qualified(shape, qsh, s, p){
     let (list = sh:path(shape, s, p)) {
        mapfindlist(sh:qualifiedShape, list, xt:list(shape), qsh)
     }
}











function sh:lessThan(shape, sh, vis, s, p, q){
    sh:lessThanList(shape, sh, vis, s, p, q, false, sh:lessThan)
}

function sh:lessThanOrEquals(shape, sh, vis, s, p, q){
    sh:lessThanList(shape, sh, vis, s, p, q, true, sh:lessThanOrEquals)
}

function sh:lessThanList(shape, sh, vis, s, p, q, leq, oper){
    let (suc = true, nodeList = xt:objects(s, q)) {
        for (o in xt:objects(s, p)) {
            for (v in nodeList) {
                let (res = coalesce(if (leq, o <= v, o < v), false)) {
                    sh:report(oper, sh, shape, s, p, o, res, vis) ;
                    if (! res) {
                        set(suc = false)
                    } 
                }
            }
        } ;
        return (suc)
    }
}


function sh:uniqueLang (shape, sh, vis, s, p, v){
    let (suc = true, amap = xt:map(),
         nodeList = sh:path(shape, s, p),
         nodeLang = mapfindlist(lambda(value) { coalesce(lang(value) != "", false) }, nodeList)) {
         
         for (value in nodeLang) {
            let (lan = lang(value)) {
                xt:set(amap, lan, coalesce(xt:get(amap, lan), 0) + 1)
            }
         } ;
         
         for ((lan value) in amap) {
            let (res = value = 1) {
                if (res, true, set(suc = false));
                sh:report(sh:uniqueLang, sh, shape, s, p, lan, res, vis)
            }
         } ;
         
        return (suc)
    }
}



function sh:hasValue (shape, sh, vis, s, p, v) {
    let (suc = sh:hasValue(v, sh:path(shape, s, p))) {
        sh:report(sh:hasValue, sh, shape, s, p, v, suc, vis) ;
        return (suc)
    }
}

function sh:hasValue (object, list) {
    for (value in list) {
        if (coalesce(object = value, false), return(true), true)
    } ;
    return (false)
}


function sh:minCount (shape, sh, vis, s, p, m) {
    let (val = xt:size(sh:path(shape, s, p)),
         suc = val >= m) { 
        sh:report(sh:minCount, sh, shape, s, p, val, suc, vis);
        return(suc)
    }
}

function sh:maxCount (shape, sh, vis, s, p, m) {
    let (val = xt:size(sh:path(shape, s, p)),
         suc = val <= m) { 
        sh:report(sh:maxCount, sh, shape, s, p, val, suc, vis);
        return(suc)
    }
}





function sh:disjoint (shape, sh, vis, s, p, q) {
    let (res = true, 
         l1 = sh:path(shape, s, p),
         l2 = sh:path(shape, s, q)) {
        for (o in l1) {
            let (suc = ! xt:member(o, l2)) {
                sh:report(sh:disjoint, sh, shape, s, p, o, suc, vis) ;
                set (res = res && suc)
            }
        } ;
        return (res)
    }
}



function sh:equals (shape, sh, vis, s, p, q) {
    let (l1 = sh:path(shape, s, p), 
         l2 = sh:path(shape, s, q)) {         
         let (b1 = sh:myequals (shape, sh, vis, s, p, q, l1, l2),
              b2 = sh:myequals (shape, sh, vis, s, p, q, l2, l1)) {
        return (b1 && b2)
        }
    }
}

function sh:myequals (shape, sh, vis, s, p, q, l1, l2) {
    let (res = true) {
        for (o in l1) {
            let (suc = xt:member(o, l2)) {
                 sh:report(sh:equals, sh, shape, s, p, o, suc, vis) ;
                 set(res = res && suc) 
            }
        } ;
        return (res)
    }
}




#
# Boolean operators 
#

function sh:not (shape, sh, vis, s, p, cst) {
    let (res = true) {
        for (o in sh:path(shape, s, p)) {
            let (suc = ! sh:core(shape, cst, false, xt:list(o))) { 
                sh:report(sh:not, sh, shape, s, p, o, suc, vis);
                set (res = res && suc)
            }
        } ;
        return (res)
    }
}

#
# basic sh:and sh:or sh:xone defined in core.rq
# cst is the bnode start list of shapes of boolean shape sh
# sh sh:and cst where cst = (sh1 .. shn)
#
function sh:and (shape, sh, vis, s, p, cst) {
    let (res = true, shList = sh:getConstraint(sh:pathboolean, shape, cst)) {
        for (o in sh:path(shape, s, p)) {
            let (suc = coalesce(sh:and(shape, shList, o), false)) { 
                if (suc, true, sh:report(sh:and, sh, shape, s, p, o, suc, vis));
                set (res = res && suc)
            }
        };
        return (res)
    }
}

function sh:or (shape, sh, vis, s, p, cst) {
    let (res = true, shList = sh:getConstraint(sh:pathboolean, shape, cst)) {
        for (o in sh:path(shape, s, p)) {
            let (suc = coalesce(sh:or(shape, shList, o), false)) { 
                if (suc, true, sh:report(sh:or, sh, shape, s, p, o, suc, vis));
                set (res = res && suc)
            }
        };
        return (res)
    }
}

function sh:xone (shape, sh, vis, s, p, cst) {
    let (res = true, shList = sh:getConstraint(sh:pathboolean, shape, cst)) {
        for (o in sh:path(shape, s, p)) {
            let (suc = coalesce(sh:xone(shape, shList, o), false)) { 
                if (suc, true, sh:report(sh:xone, sh, shape, s, p, o, suc, vis));
                set (res = res && suc)
            }
        };
        return (res)
    }
}




#
# cst is the bnode start of the list of shapes of the boolean operator
# and (sh1 .. shn) ; cst = (sh1 .. shn)
# Return the list of shapes of the boolean operator
# boolean prop = true if the constraint is a sh:property shape
#
function sh:pathboolean(shape, cst) {
     let ( 
        SELECT ?cst  ?shape (aggregate(?sh) as ?list)
		WHERE {
            graph ?shape { 
                ?cst rdf:rest*/rdf:first ?sh
            } } ) { 
            return (list)
        }
}













